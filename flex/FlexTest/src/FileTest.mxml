<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" backgroundColor="#FFFFFF">
	<mx:Script>
		<![CDATA[
			import mx.controls.dataGridClasses.DataGridColumn;
			import mx.controls.advancedDataGridClasses.AdvancedDataGridColumn;
			import mx.rpc.events.ResultEvent;
			import com.adobe.serialization.json.JSON;
			import mx.utils.ObjectUtil;
			import mx.controls.Alert;
			private var _file:FileReference = new FileReference();
			
            private var _fileInfo:Object = {};
			
			// 上传状态指示, 和下面的文本框绑定
            [Bindable]
            private var stateText:String = "请选择一个文件上传";
            
            // createChildren 比 creationComplete 事件更早发生, 省的注册事件侦听, 直接在这里写了
            protected override function createChildren():void {
                super.createChildren();
                _file.addEventListener(Event.SELECT, file_select);
                _file.addEventListener(Event.COMPLETE, file_complete);
                _file.addEventListener(ProgressEvent.PROGRESS, file_progress);
            }
            
            // 选择 1 个文件的事件
            private function file_select (e:Event):void {
                stateText = "选择了文件 " + _file.name;
            }
            
            // 上传完毕后的事件
            private function file_complete (e:Event):void {
                stateText = "上传完毕";
            }
            
            private function file_progress (e:ProgressEvent):void {
                stateText = "已上传 " + Math.round(100 * e.bytesLoaded / e.bytesTotal) + "%";
            }
            // 先判断一下文件大小, 再上传, FileService.aspx 就是上传地址
            private function upload ():void {
                if (_file.size > 0) {
                    stateText = "正在上传 " + _file.name;
                    var request:URLRequest = new URLRequest("http://192.168.32.31:8080/rp3/uploadFileTools.action?fileName="+_file.name+"&contentType="+_file.type+"&bizfield=doc-files&folderpath=upload");
                    _file.addEventListener(DataEvent.UPLOAD_COMPLETE_DATA,uploadComplete);
                    _file.upload(request,"doc",false);
                }
            }
            
            private function open():void{
            	dataGridXmlService.send({filePath:_fileInfo.fileInfo.filePath,bizfield:_fileInfo.fileInfo.bizfield,sheetIndex:sheetIndexComboBox.selectedItem.data,startRow:startRow.text});
            }
            
            private function uploadComplete(e:DataEvent):void{
            	_fileInfo = JSON.decode(e.data);
            	sheetCountService.send({filePath:_fileInfo.fileInfo.filePath,bizfield:_fileInfo.fileInfo.bizfield});
            }
			
			private function createHeaders(dataChildren:XMLList) : Array{
				var children:Array = new Array(dataChildren.length());
				for(var i:int=0;i<dataChildren.length();i++){
			    	var xml:XML = dataChildren[i];
			    	var name:String = xml.name().toString();
			    	if(name=="column"){
			    		var column:DataGridColumn = new DataGridColumn();
					    /**
					    * 给column设置属性
					    **/
			    		column.headerText=xml.@headerText;
			    		if(xml.@width.toString()!="")
			    			column.width=xml.@width;
			    		column.dataField=xml.@dataField.toString()!=""?"@"+xml.@dataField:"";
			    		column.setStyle("color",0x00000);
			    		column.setStyle("fontSize",xml.@fontSize.toString()!=""?xml.@fontSize:12);
			    		if(xml.@textAlign=="left"){
			    			column.setStyle("textAlign","left");
			    		}else if(xml.@textAlign=="center"){
			    			column.setStyle("textAlign","center");
			    		}else{
			    			column.setStyle("textAlign","right");
			    		}
			    		if(column.dataField=="@rownum"){
			    			column.editable = false;
			    		}else{
			    			column.editable = true;
			    		}
			    		
			    		children[i] = column;
			    	}
			    }
			    return children;
			}
			
			
            /**
			 * 列表数据返回后要调用的方法
			 * 先递归生成列表的表头
			 * 然后加载列表的数据
			 * 为图表加载数据
			 **/
			private function dataGridXmlServiceResult(event:ResultEvent) : void {
				var setXml:XML = (XML)(event.result);
			    if(setXml.name().toString()=="error"){
		    		Alert.show(setXml.toString(),"提示");
		    		return;
			    }
				dataGrid.columns = createHeaders(setXml.header[0].column);
				dataGrid.dataProvider = setXml.datas[0].data;
			}
			private function sheetCountServiceResult(event:ResultEvent) : void {
				var sheetCountO:Object = JSON.decode(event.result.toString());
				var sheetCount:int = sheetCountO.sheetChout;
				sheetIndex.removeAll();
				for(var i:int=0;i<sheetCount;i++){
					sheetIndex.addItem({data:(i+1), label:(i+1)});
				}
			}
			private function sendNewData():void{
				var newData:String = dataGrid.dataProvider.toString();
                var request:URLRequest = new URLRequest("http://192.168.32.31:8080/ahboot/saveNewDataFlexExcel.action");
                request.contentType='applicatoin/octet-stream';
				request.data=newData;
				request.method=URLRequestMethod.POST;
				sendToURL(request);
			}
		]]>
	</mx:Script>
	<mx:HTTPService id="dataGridXmlService" resultFormat="xml" url="http://192.168.32.31:8080/ahboot/readExcelFlexExcel.action" result="dataGridXmlServiceResult(event)" />
	<mx:HTTPService id="sheetCountService" resultFormat="text" url="http://192.168.32.31:8080/ahboot/getSheetCountFlexExcel.action" result="sheetCountServiceResult(event)" />
	<mx:DataGrid id="dataGrid" editable="true" 
		sortableColumns="false"
		horizontalScrollPolicy="on" 
		width="98%" height="98%"
		headerStyleName="header" textAlign="right" 
		color="#000000" styleName="myFont" 
		fontSize="12" rollOverColor="#EFEFEF" selectionColor="#DFE8F7" fontFamily="SIMSUN" horizontalCenter="0" y="30">
	</mx:DataGrid>
	<mx:Button label="浏览" click="{_file.browse();}" x="305" y="5"/>
	<mx:TextInput id="filePath" x="10" y="5" text="{stateText}" width="287" editable="false"/>
	<mx:Button x="361" y="5" label="上传" click="{upload();}"/>
	<mx:ComboBox id="sheetIndexComboBox" x="582" y="5" width="52">
		<mx:ArrayCollection id="sheetIndex">
			<mx:Object data="1" label="1"/>
		</mx:ArrayCollection>
	</mx:ComboBox>
	<mx:Button x="642" y="5" label="打开" click="{open();}"/>
	<mx:Label x="539" y="7" text="Sheet："/>
	<mx:TextInput id="startRow" x="486" y="5" width="42" restrict="0-9" text="1" maxChars="999"/>
	<mx:Label x="443" y="7" text="起始行："/>
	<mx:Button x="720" y="5" label="发送" click="{sendNewData();}"/>
</mx:Application>
